plugins {
    id "java"
    id "maven-publish"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
compileJava.options.encoding = "UTF-8"

archivesBaseName = project.maven_name
group = project.maven_group
version = project.maven_version

repositories {
    mavenCentral()
    maven {url = "https://repo.papermc.io/repository/maven-public/"}
    ivy {
        url "https://cdn.getbukkit.org/spigot"
        patternLayout {
            artifact "/[organisation]-[module]-latest.jar"
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        zipTree(project(":PaperSupport").jar.archiveFile)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    failOnError = false
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            url = ((project.updatable == "true") ? "https://maven.lenni0451.net/releases" : "https://maven.lenni0451.net/snapshots")
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = project.maven_name
            groupId = project.maven_group
            version = project.maven_version

            from components.java
        }
    }
}

processResources {
    inputs.properties(
            "version": project.maven_version + ((project.updatable == "false") ? "-SNAPSHOT" : "")
    )

    filesMatching("plugin.yml") {
        expand(
                "version": project.maven_version + ((project.updatable == "false") ? "-SNAPSHOT" : "")
        )
    }
}
